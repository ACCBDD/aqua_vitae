plugins {
    id 'java-library'
    id 'maven-publish'
    id 'idea'
    id 'net.neoforged.moddev' version '2.0.116'
}

version = mod_version
group = mod_group_id

repositories {
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        url = "https://api.modrinth.com/maven"
    }
    maven {
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        name = "ModMaven"
        url = "https://modmaven.dev"
    }
    maven {
        // EMI
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/"
    }
    maven {
        // REI
        url "https://maven.shedaniel.me/"
    }
    mavenLocal()
    flatDir {
        dir 'libs'
    }
}

base {
    archivesName = mod_id + "-" + minecraft_version
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

neoForge {
    version = project.neo_version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

    // This line is optional. Access Transformers are automatically detected
    // accessTransformers.add('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            client()

            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        gameTestServer {
            type = "gameTestServer"
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()

            programArguments.addAll '--mod', project.mod_id, '--all',
                    '--output', file('src/generated/resources/').getAbsolutePath(),
                    '--existing', file('src/main/resources/').getAbsolutePath()
        }

        configureEach {
            // Recommended logging data for a userdev environment
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            systemProperty 'forge.logging.markers', 'REGISTRIES'

            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

tasks.named('runClient') {
    classpath += configurations.devRuntime
}

tasks.named('runServer') {
    classpath += configurations.devRuntime
}

configurations {
    devRuntime
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    compileOnly "mezz.jei:jei-${minecraft_version}-common-api:${jei_version}"
    compileOnly "mezz.jei:jei-${minecraft_version}-neoforge-api:${jei_version}"
    compileOnly "dev.emi:emi-neoforge:${emi_version}:api"

    implementation "maven.modrinth:jade:${jade_version}"

    //compat testing
    devRuntime "curse.maven:appleskin-248787:${appleskin_id}"
    devRuntime "curse.maven:mekanism-268560:7132168"
    devRuntime "curse.maven:create-328085:7178775"
    devRuntime "curse.maven:fruits-delight-943774:7041466"
    devRuntime "curse.maven:farmers-delight-398521:6917256"

    if (runtime_recipe_viewer == "jei") {
        runtimeOnly "mezz.jei:jei-${minecraft_version}-neoforge:${jei_version}"
    } else if (runtime_recipe_viewer == "rei") {
        runtimeOnly "me.shedaniel:RoughlyEnoughItems-neoforge:${rei_version}"
    } else if (runtime_recipe_viewer == "emi") {
        runtimeOnly "dev.emi:emi-neoforge:${emi_version}"
    } else if (runtime_recipe_viewer == "emi+") {
        runtimeOnly "mezz.jei:jei-${minecraft_version}-neoforge:${jei_version}"
        runtimeOnly "dev.emi:emi-neoforge:${emi_version}"
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [minecraft_version      : minecraft_version,
                             minecraft_version_range: minecraft_version_range,
                             neo_version            : neo_version,
                             neo_version_range      : neo_version_range,
                             loader_version_range   : loader_version_range,
                             mod_id                 : mod_id,
                             mod_name               : mod_name,
                             mod_license            : mod_license,
                             mod_version            : mod_version,
                             mod_authors            : mod_authors,
                             mod_description        : mod_description]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}

sourceSets.main.resources.srcDir generateModMetadata
neoForge.ideSyncTask generateModMetadata

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
